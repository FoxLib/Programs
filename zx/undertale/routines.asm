
; ----------------------------------------------------------------------
; Обработка события IM 2
; ----------------------------------------------------------------------

IM2:        push    hl
            push    de
            push    bc
            push    af
            push    ix

IM2LOOP:    ld      ix, KEvent      ; Прочесть значение задержки
            ld      a, (ix+0)       ; Время до окончания ожидания
            and     a
            jr      nz, IM2NEXT     ; Все еще есть ожидание события?
            ld      hl, (EVAddr)    ; Извлечь следующее событие из потока
            ld      a, (hl)
            inc     hl
            ld      (ix+0), a       ; Запись нового delay
            ld      a, (hl)
            inc     hl
            ld      (ix+1), a       ; Тип события
            ld      (EVAddr), hl    ; Сохранить новый указатель
            and     a
            jr      z, IM2LOOP      ; 00 Ничего не делать
            cp      $01
            jr      z, IM2WRCHR     ; 01 Печать символа
            jr      $               ; Любое нераспознанное событие: остановка

IM2NEXT:    dec     (ix+0)
IM2END:     pop     ix
            pop     af
            pop     bc
            pop     de
            pop     hl
            ei
            ret

; ----------------------------------------------------------------------
; ТИП 01: Пропечатать символ
; ----------------------------------------------------------------------

IM2WRCHR:   ld      hl, (NEXTSYM)       ; Извлечь следующий символ
            ld      a, (hl)
            inc     hl
            ld      (NEXTSYM), hl
            ld      hl, (NEXTPOS)       ; Следующая позиция
            call    PRN
            ld      (NEXTPOS), hl
            jr      IM2LOOP

; ----------------------------------------------------------------------
; Декомпрессия следующей картинки
; ----------------------------------------------------------------------

NEXTSCR:    ld      hl, (SCRPTR)        ; Получить указатель в таблице
            ld      e, (hl)
            inc     hl
            ld      d, (hl)
            inc     hl                  ; DE=адрес, где лежит картинка
            ld      (SCRPTR), hl        ; К следующему указателю
            ex      de, hl              ; Теперь адрес находится в HL
            ld      de, DECOMP          ; Адрес декомпрессии в DE
            call    dzx0
            ret

; ----------------------------------------------------------------------
; Печать одного символа A в режиме телетайпа -> HL
; ----------------------------------------------------------------------

PRN:        push    hl
            push    de
            push    bc
            ex      de, hl          ; Сохранить HL
            ld      bc, FONTS
            ld      l, a
            ld      h, 0
            add     hl, hl
            add     hl, hl
            add     hl, hl
            add     hl, hl
            add     hl, bc
            ex      de, hl          ; DE = 16*A + FONTS
            ld      c, 2
PRN2:       ld      b, 8
PRN1:       ld      a, (de)         ; Нарисовать половину символа
            ld      (hl), a
            inc     de
            inc     h
            djnz    PRN1
            ld      a, h            ; Вернуть H на место
            sub     $08
            ld      h, a
            ld      a, l
            add     $20             ; Проверка на превышение L
            jr      nc, PRN3
            ex      af, af'
            ld      a, h
            add     $08             ; Перенос к следующему блоку
            ld      h, a
            ex      af, af'
PRN3:       ld      l, a
            dec     c
            jr      nz, PRN2
            pop     bc
            pop     de
            pop     hl
            inc     l
            ret

; ----------------------------------------------------------------------
; Отрисовка картинки HL, A-затемнение [0..8]
; ----------------------------------------------------------------------

REDRAW:     push    hl              ; Сохранить для повторного использования
            push    af
            ld      iyl, a
            ld      iyh, 0
            ld      bc, DMASK0
            add     iy, iy
            add     iy, iy
            add     iy, iy
            add     iy, bc          ; IX = DMASK0 + 8*A
            ld      c, 104          ; 104 строки
            ld      (IYhold), iy    ; Сохранение указателя дизеринга
            ld      ix, YTABLE      ; Предвычисленная таблица Y-позиции
            ld      a, 8            ; Для циклического вращения IY
            ex      af, af'
DRAW2:      ld      d, (ix+1)       ; Следующий табличный адрес [0..103]
            ld      e, (ix+0)
            inc     ix
            inc     ix
            ld      a, (iy+0)       ; Маска дизеринга для затемнения
            ld      (MODIF+1), a    ; Модифицировать код для наложения маски
            ld      b, 25           ; 25 x 8 = 200 пикселей
; --- 1145T Рисование одной линии -----
DRAW1:      ld      a, (hl)         ; 7T Основной цикл рисования
MODIF:      or      $00             ; 7T Самомодицифирующийся код
            ld      (de), a         ; 7T
            inc     de              ; 6T
            inc     hl              ; 6T
            djnz    DRAW1           ; 13T/8T
; -------------------------------------
            inc     iy              ; К следующему циклу
            ex      af, af'
            dec     a
            jr      nz, DRAW3       ; Проверить, что не достигло 0
            ld      iy, (IYhold)    ; Восстановить IY
            ld      a, 8            ; Восстановить счетчик
DRAW3:      ex      af, af'
            dec     c               ; К следующей линии
            jr      nz, DRAW2
            pop     af
            pop     hl
            ret

; ----------------------------------------------------------------------
; Заливка области картинки (A-цвет $30)
; ----------------------------------------------------------------------

DRAWBG:     ld      hl, $5803
            ld      de, 7
            ld      c, 13
LINE2:      ld      b, 25
LINE1:      ld      (hl), a
            inc     hl
            djnz    LINE1
            add     hl, de
            dec     c
            jr      nz, LINE2
            ret
